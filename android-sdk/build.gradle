apply plugin: 'com.android.library'

ext {
    sdkOrg = 'blueshift-labs'
    sdkGroup = 'com.blueshift'
    sdkArtifactName = 'android-sdk-test'

    sdkVersion = '3.1.2-alpha'
    sdkVersionDesc = 'Blueshift Android SDK v3.1.2-alpha'
    sdkVcsTag = '3.1.2-alpha'

    sdkName = 'Blueshift Android SDK'
    sdkDesc = 'Blueshift Android SDK to make it easier for marketers to integrate their mobile app with Blueshiftâ€™s near real-time event processing platform and send personalized rich push notifications to their customers.'
    sdkSite = 'https://blueshift.com'
    sdkVcsUrl = 'https://github.com/blueshift-labs/Blueshift-Android-SDK.git'

    sdkDevId = 'rahulrvp'
    sdkDevName = 'Rahul Raveendran V P'
    sdkDevEmail = 'rahul.raveendran@blueshift.com'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'SDK_VERSION', "\"$sdkVersion\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // for JSON parsing
    implementation 'com.google.code.gson:gson:2.8.6'

    // for FCM
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    testImplementation 'junit:junit:4.13'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // todo: remove below line when fixes are applied for excluding external aars for javadoc.
    // failOnError = false;
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id sdkDevId
            name sdkDevName
            email sdkDevEmail
        }
    }

    scm {
        url sdkVcsUrl
    }
}

// Create the publication with the pom configuration:
publishing.publications {
    aar(MavenPublication) {
        artifact sourcesJar
        artifact javadocJar
        groupId sdkGroup
        artifactId sdkArtifactName
        version sdkVersion
        pom {
            packaging 'aar'
            withXml {
                def root = asNode()
                root.appendNode('description', sdkDesc)
                root.appendNode('name', sdkName)
                root.appendNode('url', sdkSite)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['aar']

    pkg {
        repo = 'maven'
        name = sdkArtifactName
        userOrg = sdkOrg
        licenses = ['MIT']
        vcsUrl = sdkVcsUrl

        version {
            name = sdkVersion
            desc = sdkVersionDesc
            released = new Date()
            vcsTag = sdkVcsTag

            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_GPG')
            }
        }
    }
}
