apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.vanniktech.maven.publish'

ext {
    PUBLISH_GROUP_ID = 'com.blueshift'
    PUBLISH_ARTIFACT_ID = 'android-sdk-x'
    PUBLISH_VERSION = '4.0.1'
}

android {
    namespace "$PUBLISH_GROUP_ID"
    compileSdk 34

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'SDK_VERSION', "\"$PUBLISH_VERSION\""
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.13.0'
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'com.google.truth:truth:1.1.4'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'androidx.security:security-crypto:1.0.0'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.4.4'
    implementation 'com.google.firebase:firebase-messaging:20.3.0'

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation("androidx.test:core:1.5.0")
    testImplementation("androidx.arch.core:core-testing:2.2.0")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3")
    testImplementation("com.google.truth:truth:1.1.4")
    testImplementation("io.mockk:mockk:1.13.5")
    testImplementation("org.json:json:20140107")
}

tasks.register('androidJavadoc', Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += files(project.android.bootClasspath)

    // This task works only for java classes atm. Not for Kotlin objects.
    // So, we exclude the Kotlin files from this task.
    exclude '**/*.kt'

    failOnError false
}

tasks.register('androidJavadocsJar', Jar) {
    dependsOn androidJavadoc
    archiveClassifier.set('javadoc')
    from androidJavadoc.destinationDir
}

mavenPublishing {
    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

    pom {
        name = PUBLISH_ARTIFACT_ID
        description = "Blueshift Android SDK"
        inceptionYear = "2021"
        url = 'https://github.com/blueshift-labs/Blueshift-Android-SDK'
        licenses {
            license {
                name = 'MIT'
                url = 'https://opensource.org/licenses/mit-license.php'
            }
        }
        developers {
            developer {
                id = "rishujam"
                name = "Sudhanshu Kumar"
                email = "androiddeveloper@getblueshift.com"
            }
        }
        scm {
            url = "https://github.com/blueshift-labs/Blueshift-Android-SDK/tree/main"
            connection = "scm:git:github.com/blueshift-labs/Blueshift-Android-SDK.git"
            developerConnection = "scm:git:ssh://github.com/blueshift-labs/Blueshift-Android-SDK.git"
        }
    }
}